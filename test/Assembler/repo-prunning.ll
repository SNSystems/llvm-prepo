; This testcase tests the program repository pruning pass. If the global
; variables or functions have been defined in the database 'clang.db', they
; will change from the definition to declaration.
;
; The testcase includes three steps:
; Step 1: Generate the repo IR code which contains the TicketNode metadata;
; Step 2: Create the database 'clang.db' which contains all Tickets.
; Step 3: Run the ProgramRepositoryPrunningPass to optimise the repo IR code
;         which is generated by Step 1;

; RUN: rm -f %t.db
; RUN: env REPOFILE=%t.db opt -mtriple="x86_64-pc-linux-gnu-repo" -O0 -S %s -o %t
; RUN: llc -mtriple="x86_64-pc-linux-gnu-repo" -filetype=obj %t
; RUN: opt -mtriple="x86_64-pc-linux-gnu-repo" -O0 -S %t | FileCheck %s

target triple = "x86_64-pc-linux-gnu-elf"

;CHECK: @c = external global i32, align 4, !repo_ticket !0
$c = comdat any
@c = global i32 8, align 4, comdat($c)
;CHECK: @a = external global i32, align 4, !repo_ticket !1
$a = comdat exactmatch
@a = global i32 1, align 4, comdat($a)
;CHECK: @b = external global i32, align 4, !repo_ticket !2
$b = comdat largest
@b = internal global i32 1, comdat, align 4, comdat($b)

;CHECK: declare !repo_ticket !3 i8* @me()
$me = comdat noduplicates
define linkonce_odr i8* @me() comdat($me) {
entry:
  ret i8* bitcast (i8* ()* @me to i8*)
}

;CHECK: declare !repo_ticket !4 i32 @foo()
define internal i32 @foo() {
entry:
  %0 = load i32, i32* @a, align 4
  %inc = add nsw i32 %0, 1
  store i32 %inc, i32* @a, align 4
  store i32 2, i32* @b, align 4
  store i32 2, i32* @c, align 4
  %1 = load i32, i32* @a, align 4
  ret i32 %1
}

;CHECK: declare !repo_ticket !5 i32 @bar()
define i32 @bar() {
entry:
  %call = call i32 @foo()
  ret i32 %call
}

;CHECK:      !0 = !TicketNode(name: "c", digest: [16 x i8] c"\84\E5\7F\EE\D7S^\A54R\93H*[\188", linkage: external, pruned: true)
;CHECK-NEXT: !1 = !TicketNode(name: "a", digest: [16 x i8] c"\82\8E:\15)\9F\13W\12\04l\0C+&z\11", linkage: external, pruned: true)
;CHECK-NEXT: !2 = !TicketNode(name: "b", digest: [16 x i8] c"\82\8E:\15)\9F\13W\12\04l\0C+&z\11", linkage: internal, pruned: true)
;CHECK-NEXT: !3 = !TicketNode(name: "me", digest: [16 x i8] c">\F2\04\C1\8F?\83ZF\1Ey\02~)\F1\A2", linkage: linkonce_odr, pruned: true)
;CHECK-NEXT: !4 = !TicketNode(name: "foo", digest: [16 x i8] c"Ul\EA\B1\FE\16\C8\00FC:\B8\12E\06=", linkage: internal, pruned: true)
;CHECK-NEXT: !5 = !TicketNode(name: "bar", digest: [16 x i8] c"+\92]\8D?\94\14\E9\E7\A3\B9\93\91A\C0d", linkage: external, pruned: true)
